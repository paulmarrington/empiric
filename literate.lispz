### spec: literate - Literate Programming
  **Lispz** supports a form of Donald Knuth's Literate programming. Use the
  *literate* annotation and document to your heart's content. The line of
  the annotation is the title.

  The body of the documentation is written in Github style markdown with all
  the typographic features markdown provides.
###
(using [net cdnjs dict list]
  (ref build (lambda [target-repo]
    (return (cdnjs.build target-repo "showdown" [[
      {repo: "showdown" files: [[
        {include: '/showdown.js$/'}
      ]]}
    ]]))
  ))
  (ref loaded (net.script "ext/showdown.js"(lambda [] (return window.showdown))))
  (catch loaded [] (export {build}))
  (when  loaded []
    ### spec: literate >> to-html
      The core process is to initialise the markdown to html compiler provide
      the conversion with ```to-html```. The options default if not provided.
      The full list is available on
      [the showdown wiki](https://github.com/showdownjs/showdown#valid-options).

          (using literate
            ## markdown list is [[{title: "One" body: markdown} ...]]
            (literate.to-html markdown-list)
            ## or
            (literate.to-html markdown-list {headerLevelStart: 1})
          )

      For the lowdown on the markdown syntax provided, have a gander at
      [The Showdown Wiki](https://github.com/showdownjs/showdown/wiki/Showdown's-Markdown-syntax)
      or check out the Jasmine tests below.
    ###
    (ref to-html (lambda [sections options]
      (ref seed (lambda [] (return sections)))
      (ref sections (chain seed parse-titles sort-by-title merge-titles))
      (ref markdown (sections.map (lambda [section]
        (return (section.markdown.push section.body))
      )))
      (return ((converter options).makeHtml ((list.flatten markdown).join "")))
    ))
    ### spec: literate >> options
      You can pass in a dictionary of options that will change how markdown
      is converted to HTML. Most are explicit:

          omitExtraWLInCodeBlocks, noHeaderId, prefixHeaderId, headerLevelStart,
          simplifiedAutoLink, literalMidWordUnderscores, strikethrough, tables,
          tablesHeaderId

      while others require a tiny bit of explanation:

          parseImgDimensions - ![foo](foo.jpg =100x80) px, em, % or * for auto
          ghCodeBlocks - triple-backtick instead of indenting
          smoothLivePreview - not used by Lispz at this point
    ###
    (ref converter (lambda [options]
      (ref defaults {
        omitExtraWLInCodeBlocks:    false
        noHeaderId:                 false
        prefixHeaderId:             false
        parseImgDimensions:         false   ## 100x80, 100x*, 80%x5em
        headerLevelStart:           2
        simplifiedAutoLink:         true    ## GFM recognizing urls in text
        literalMidWordUnderscores:  false
        strikethrough:              true    ## ~~strikethrough~~
        tables:                     true    ## |h1|h2... |:--... |te1|te2
        tablesHeaderId:             true
        ghCodeBlocks:               true    ## GFM style code-blocks
        smoothLivePreview:          false
      })
      (return (new showdown.Converter (dict.merge defaults (or options {}))))
    ))
    ### spec: literate >> title-syntax -- Title Syntax
      Literate programming requires the documentation to be with the code.
      In most cases the code clearly defines the how without additional
      comments. Thw why, however, is still in the province of documentation.
      Also, documentation when being read later is usually in a different order
      to the code it is following. For this reason, literate annotations
      include titles - on the same line as the annotation.

      Sections are reference by a key word, with a double-right-arrow
      between inner sections.

            ##(#) spec: test -- The title of a test section
            ##(#) spec: test >> details -- A detailed test title
            ##(#) spec: test >> details >> more

      To make it easier to repeat titles, they are key-word references.
      Before they can be used they need to expand to the full title.
      At some point when that title section is the last on the chain,
      append a double-minus and the full title name.

          ##(#) spec: test >> details >> more -- More Details
    ###
    (ref titles (stateful {}))
    (ref get-title (pull titles))

    (ref parse-titles (lambda [sections]
      (return (sections.map (lambda [section]
        (ref title ((section.title.split '/\s+>>\s+/').map (lambda [part]
          (ref split (part.split '/\s+--\s+/'))
          (return-if (is split.length 1) part)
          (ref key (first split)  title (rest split))
          (titles.update! key title)
          (return key)
        )))
        (return {title sort: (title.join " ") body: section.body})
      )))
    ))

    ### spec: literate >> sort-by-title -- Sort document by title
      Literate sections in the soure may need to be put together in a
      different order to make the document. In this way sections will
      only show once, even if they are referenced in different places
      when defining sub-sections.
    ###
    (ref sort-by-title (lambda [sections]
      ### spec:
        Note that this changes the contents of the parameter - not perfect
        but acceptable since the array was created by the preceding function.
      ###
      (sections.sort (lambda [a b] (a.sort.localeCompare b.sort)))
      (return sections)
    ))
    ### spec: literate >> merge-titles -- Merge titles into the Markdown
      In the end a title is a title - being lines starting with one or more
      hashes where the number indicates the heading level. We must inspect
      each title and remove the common ancestores before printing the
      remaining title lines.
    ###
    (ref merge-titles (lambda [sections]
      (ref context (stateful.morph {parent: [[]]}))
      (ref in-parent-context (pull context.parent))
      (return (sections.map (lambda [section]
        (ref markdown [[]])
        (section.title.forEach (lambda [key idx]
          (cond (is (in-parent-context idx) key)
            (return (parent.update! (parent: [[]]}))
          )
          (ref title (get-title key)  hashes ("######".slice 0 (+ idx 1)))
          (markdown.push hashes " " title "\n")
        ))
        (return {markdown body: section.body})
      )))
    ))
    (export {build to-html})
  )
)
